plugins {
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
    id 'application'}

group 'jonkerridge'
version '1.0.9'
//1.0.0 first working version that was tested with Mandelbrot and Concordance
//1.0.1 improved means of invoking Host and Node processes
//1.0.2 tested on real networks, further improvement in invoking host process
//1.0.3 eradicated parser version checking error
//1.0.4 introduced the static version control method
//1.0.5 enables multiple collate and finalise methods in multi Collector CollectNodes
//1.0.6 ensure output timing file contained host timings as well
//1.0.7 documentation improved and created Java 8 version using Groovy3 for ENUCC
//1.0.8 added a delay so that nodes wait until host has sufficient time to start.
//1.0.9 added verbose to invoke to reduce amount of console output

String propertyFileName = "gradle.properties"
File propsFile = new File(propertyFileName)
Properties props = new Properties()
props.load(propsFile.newDataInputStream())

repositories {
    mavenCentral()
    mavenLocal()  // needed to load groovy_jcsp.1.1.9
    maven { // to download the jonkerridge.groovy_jcsp library
        name = "GitHubPackages"
        url = "https://maven.pkg.github.com/JonKerridge/groovyJCSP"
        credentials {
            username = props.getProperty("gpr.user")
            password = props.getProperty("gpr.key")
        }
    }
    maven { // to download the cspforjava.jcsp library
        name = "GitHubPackages"
        url = "https://maven.pkg.github.com/CSPforJAVA/jcsp"
        credentials {
            username = props.getProperty("gpr.user")
            password = props.getProperty("gpr.key")
        }
    }
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:3.0.22'
//    implementation 'org.apache.groovy:groovy-all:4.0.21'
    implementation 'cspforjava:jcsp:1.1.9'
    implementation 'jonkerridge:groovy_jcsp:1.1.10'
//    implementation 'org.codehaus.groovy:groovy-cli-picocli:3.0.18'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}


task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set( "sources")
}

artifacts {
    archives sourcesJar
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/Pannoniae/cluster_cli"
            credentials {
                username = props.getProperty("gpr.user")
                password = props.getProperty("gpr.key")
            }
        }
    }
    publications {
        clic(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}